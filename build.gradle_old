plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = stonecutter.current.project
}

println()
println "Minecraft: ${stonecutter.current.project} (Java ${java})"
println "  Platforms: ${enabled_platforms}"
println "  Fabric: ${findProperty("fabric_api")}"
println "  NeoForge: ${findProperty("neoforge")}"
println "  Forge: ${findProperty("forge")}"
println()

subprojects {
    apply plugin: 'dev.architectury.loom'

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com" }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: 'architectury-plugin'

    archivesBaseName = "$rootProject.display_name-$project.name"

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.toVersion(java)
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release.set Integer.parseInt(java)
        }
    }
}